apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 29

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "com.adonai.manman"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 175
        versionName "2.2.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile file('misc/signing.keystore')
            storePassword System.getenv('SIGNING_KEYSTORE_PASSWORD')
            keyAlias 'release-key'
            keyPassword System.getenv('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "purity"
    productFlavors {
        fdroid {
            dimension "purity"
        }

        googleplay {
            dimension "purity"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

play {
    serviceAccountCredentials = file("misc/android-publisher-account.json")
    track = 'production'
}

dependencies {
    // kotlin-related stuff
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'     // coroutines lifecycle scopes

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'             // support library (ViewPager etc.)
    implementation 'androidx.appcompat:appcompat:1.3.0'                  // support library (material design)
    implementation 'androidx.cardview:cardview:1.0.0'                    // support library (material design)
    implementation 'androidx.preference:preference-ktx:1.1.1'            // preference fragment compatibility
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.github.zhanghai:AndroidFastScroll:v1.1.5'        // recyclerview fast scroll
    // support library (material design)
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'     // progressbar on chapter load
    implementation 'com.j256.ormlite:ormlite-android:5.1'                      // ORM for man pages
    implementation 'com.google.code.gson:gson:2.8.6'                           // Json (De)Serializer
    implementation 'org.jsoup:jsoup:1.12.1'                                    // html DOM parser
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'  // file encoding detector
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'                         // good android http client
    implementation 'org.apache.commons:commons-compress:1.13'                  // uncompress lib (for archives)

    googleplayImplementation("com.android.billingclient:billing:2.1.0")

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.awaitility:awaitility:3.0.0'                        // useful wait-for-condition
    // androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
}

